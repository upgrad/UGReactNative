apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.upgrad'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

repositories {
    maven {
        url "$rootDir/node_modules/react-native/android"
    }
    //mavenLocal() must be listed at the top to facilitate testing
    mavenLocal()
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter()
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'android/main/AndroidManifest.xml'
            java.srcDirs = ['android/main/java']
            resources.srcDirs = ['android/main/java']
            aidl.srcDirs = ['android/main/java']
            renderscript.srcDirs = ['android/main/java']
            res.srcDirs = ['android/main/res']
            assets.srcDirs = ['android/main/assets']
        }
    }
}

configurations {
    pomDependencies
    //To fix: Program type already present: javax.annotation.RegEx$Checker
    compile.exclude group: 'javax.annotation'
}

apply from: 'https://raw.githubusercontent.com/kerry/gradle-fat-aar/master/fat-aar.gradle'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    embedded 'com.facebook.react:react-native:0.57.8'

    pomDependencies "com.facebook.infer.annotation:infer-annotation:0.11.2@jar"
    pomDependencies "javax.inject:javax.inject:1"
    pomDependencies "com.android.support:appcompat-v7:28.0.0"
    pomDependencies "com.facebook.fresco:fresco:1.10.0"
    pomDependencies "com.facebook.fresco:imagepipeline-okhttp3:1.10.0"
    pomDependencies "com.facebook.soloader:soloader:0.5.1"
    pomDependencies "com.google.code.findbugs:jsr305:3.0.2"
    pomDependencies "com.squareup.okhttp3:okhttp-urlconnection:3.11.0"
    pomDependencies "com.squareup.okio:okio:1.14.0"
    pomDependencies "org.webkit:android-jsc:r174650"

    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

task buildReactNative {
    doLast {
        exec {
            commandLine("/usr/local/bin/react-native", "bundle", "--platform", "android", "--dev", "false",
                    "--reset-cache", "--entry-file", "./react_native/index.js", "--bundle-output", "./android/main/assets/index.android.bundle", "--assets-dest", "./android/main/res/")
        }
    }
}

publishing {
    publications {
        ugreactnative(MavenPublication) {
            groupId 'com.upgrad'
            artifactId 'UGReactNative'
            version '0.0.1'
            artifact("$buildDir/outputs/aar/UGReactNative-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.pomDependencies.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

project.afterEvaluate {
    preBuild.dependsOn buildReactNative
    publishToMavenLocal.dependsOn assembleRelease
}
